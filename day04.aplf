⍝ AoC 2025 - Day 04 - Printing Department

⍝ Read input as character grid with padding
ParseInput←{r←↑⊃⎕NGET ⍵ 1 ⋄ c←2+1⊃⍴r ⋄ b←(1,c)⍴'.' ⋄ b⍪←('.',r,'.') ⋄ b⍪(1,c)⍴'.'}

⍝ Count @ neighbors for all positions at once
CountAllNeighbors←{n←'@'=⍵ ⋄ (¯1⌽¯1⊖n)+(¯1⊖n)+(1⌽¯1⊖n)+(¯1⌽n)+(1⌽n)+(¯1⌽1⊖n)+(1⊖n)+(1⌽1⊖n)}

⍝ Part 1: Count initially accessible rolls
Part1←{+/,('@'=⍵)∧4>CountAllNeighbors ⍵}

⍝ Part 2: Iteratively remove accessible rolls until none remain
Part2←{g←⍵ ⋄ t←0 ⋄ {r←'@'=g ⋄ 0=+/,r:t ⋄ a←r∧4>CountAllNeighbors g ⋄ n←+/,a ⋄ 0=n:t ⋄ g[⍸a]←'.' ⋄ t+←n ⋄ ∇⍬}⍬}

⍝ Read input files
input←ParseInput'2025/data/inputs/04.txt'
example←ParseInput'2025/data/examples/04.txt'

⎕←'Part 1 Example: ',⍕Part1 example ⍝ should be 13
⎕←'Part 2 Example: ',⍕Part2 example ⍝ should be 43
⎕←'Part 1: ',⍕Part1 input  ⍝ should be 1363
⎕←'Part 2: ',⍕Part2 input  ⍝ should be 8184
