⍝ AoC 2025 - Day 04 - Printing Department

⍝ Read input as numeric grid with padding (1=@, 0=.)
ParseInput←{r←'@'=↑⊃⎕NGET ⍵ 1 ⋄ c←2+1⊃⍴r ⋄ b←(1,c)⍴0 ⋄ b⍪←(0,r,0) ⋄ b⍪(1,c)⍴0}

⍝ Count neighbors for all positions at once
CountAllNeighbors←{(¯1⌽¯1⊖⍵)+(¯1⊖⍵)+(1⌽¯1⊖⍵)+(¯1⌽⍵)+(1⌽⍵)+(¯1⌽1⊖⍵)+(1⊖⍵)+(1⌽1⊖⍵)}

⍝ Part 1: Count initially accessible rolls
Part1←{+/,⍵∧4>CountAllNeighbors ⍵}

⍝ Part 2: Iteratively remove accessible rolls until none remain
Part2←{+/,⍵-{g←⍵ ⋄ a←g∧4>CountAllNeighbors g ⋄ g[⍸a]←0 ⋄ g}⍣≡⍵}

⍝ Read input files
input←ParseInput'2025/data/inputs/04.txt'
example←ParseInput'2025/data/examples/04.txt'

⎕←'Part 1 Example: ',⍕Part1 example ⍝ should be 13
⎕←'Part 2 Example: ',⍕Part2 example ⍝ should be 43
⎕←'Part 1: ',⍕Part1 input  ⍝ should be 1363
⎕←'Part 2: ',⍕Part2 input  ⍝ should be 8184
