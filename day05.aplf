⍝ AoC 2025 - Day 05 - Cafeteria

ParseInput←{
    l←⊃⎕NGET ⍵ 1 ⋄ lines←(0<≢¨l)⊆l  ⍝ Read and partition by non-empty lines
    ranges←↑{⍎' '@(⍸⍵='-')⊢⍵}¨⊃lines  ⍝ Parse ranges: replace '-' with ' ' and execute
    ids←⍎¨⊃⌽lines  ⍝ Parse IDs from last partition
    ranges ids
}

Part1←{
    ranges ids←⍵
    +/∨⌿(ranges[;1]∘.≤ids)∧ranges[;2]∘.≥ids  ⍝ Count IDs in any range
}

Part2←{
    sorted←(⊃⍵)[⍋(⊃⍵)[;1];]
    Merge←{
        1≥⊃⍴⍵:⍵
        s e←⍵[1;] ⋄ ns ne←⍵[2;]
        ns>e+1:⍵[1;]⍪∇1↓⍵
        2=⊃⍴⍵:1 2⍴s(e⌈ne)
        ∇(s(e⌈ne))⍪2↓⍵
    }
    +/{1+⍵[2]-⍵[1]}¨↓Merge sorted
}

input←ParseInput'2025/data/inputs/05.txt'
example←ParseInput'2025/data/examples/05.txt'

⎕PP←17  ⍝ Set print precision to avoid scientific notation
⎕←'Part 1 Example: ',⍕Part1 example ⍝ should be 3
⎕←'Part 2 Example: ',⍕Part2 example ⍝ should be 14
⎕←'Part 1: ',⍕Part1 input  ⍝ should be 652
⎕←'Part 2: ',⍕Part2 input  ⍝ should be 341753674214273
