⍝ AoC 2025 - Day 05 - Cafeteria

⎕CT←0  ⍝ Use exact comparison (no tolerance) for large integers

ParseInput←{
    lines←((0<≢¨)⊆⊢)⊃⎕NGET ⍵ 1  ⍝ Read and partition by non-empty lines
    ({⍵[⍋⍵[;1];]}↑{⍎' '@(⍸⍵='-')⊢⍵}¨⊃lines)(⍎¨⊃⌽lines)  ⍝ Parse, sort, and return with IDs
}

Part1←{
    ranges ids←⍵
    +/∨⌿(ranges[;1]∘.≤ids)∧ranges[;2]∘.≥ids  ⍝ Count IDs in any range
}

Part2←{
    Merge←{
        1≥⊃⍴⍵:⍵
        s e←⍵[1;] ⋄ ns ne←⍵[2;]
        ns>e:(1 2⍴s,e)⍪∇1↓⍵  ⍝ No overlap - keep separate
        ∇(1 2⍴s(e⌈ne))⍪2↓⍵  ⍝ Merge and continue
    }
    +/1--⌿⍉Merge⊃⍵
}

example←ParseInput'2025/data/examples/05.txt'
input←ParseInput'2025/data/inputs/05.txt'

⎕PP←17  ⍝ Set print precision to avoid scientific notation
⎕←'Part 1 Example: ',⍕Part1 example ⍝ should be 3
⎕←'Part 2 Example: ',⍕Part2 example ⍝ should be 14
⎕←'Part 1 input: ',⍕Part1 input  ⍝ should be 652
⎕←'Part 2 input : ',⍕Part2 input  ⍝ should be 341753674214273
⎕←'Done'