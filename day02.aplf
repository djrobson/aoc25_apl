⍝ AoC 2025 - Day 02 - Gift Shop

⍝ Parse a comma-separated list of ranges like "11-22,95-115,998-1012"
ParseRanges ← {
    nums←⊃(⎕VFI{⍵∊'-,':' ' ⋄ ⍵}¨⍵)[2]
    (2÷⍨≢nums)2⍴nums
}

⍝ Find all pattern numbers in a range [start,end] with up to maxReps repetitions
⍝ Takes a 3-element vector [start,end,maxReps]
⍝ Generates only base patterns that will produce repeating numbers within range
FindPatterns ← {
    (start end maxReps)←⍵
    minDigits maxDigits←≢¨⍕¨start end
    +/∪∊{
        patLen minValidBase←⍵(10*⍵-1)
        ∊{
            reps numDigits←⍵(⍵×patLen)
            (numDigits<minDigits)∨numDigits>maxDigits:⍬
            mult←+/10*(patLen×⍳reps)-patLen
            minBase maxBase←((⌈(start-1)÷mult)⌈minValidBase)((⌊end÷mult)⌊(minValidBase×10)-1)
            mult×(minBase-1)+⍳1+maxBase-minBase
        }¨1↓⍳maxReps⌊⌈maxDigits÷patLen
    }¨⍳maxDigits
}

⍝ Part 1: Sum all invalid IDs (doubled patterns) across all ranges
⍝ Input is an N×2 matrix where each row is [start,end]
Part1 ← {+/FindPatterns¨(↓⍵),¨2}  ⍝ maxReps=2 for doubled patterns only

⍝ Part 2: Sum all invalid IDs (any repeating patterns) across all ranges
⍝ Input is an N×2 matrix where each row is [start,end]
Part2 ← {+/FindPatterns¨(↓⍵),¨10}  ⍝ maxReps=10 for all repeating patterns (max digits in input)

⍝ Read and parse input files
input ← ParseRanges⊃⊃⎕NGET '2025/data/inputs/02.txt' 1
example ← ParseRanges⊃⊃⎕NGET '2025/data/examples/02.txt' 1

⍝ Test with example input
⎕PP←17  ⍝ Set print precision to show full numbers
⎕← 'Part 1 Example: ',⍕Part1 example
⎕← 'Part 2 Example: ',⍕Part2 example

⍝ Run on actual input
⎕← 'Part 1: ',⍕Part1 input
⎕← 'Part 2: ',⍕Part2 input
