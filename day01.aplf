⍝ AoC 2025 - Day 01 - Secret Entrance - Safe Dial Rotation

⍝ Parse a rotation instruction (e.g., "L68" or "R48")
⍝ Returns signed distance: negative for L, positive for R
ParseRotation ← (¯1+2×'R'=⊃)×⍎∘(1∘↓)

⍝ Part 1: Count times dial points at 0 AFTER rotations
Part1 ← {+/0=1↓(100∘|+)\50,⍵}

⍝ Part 2: Count all times dial points at 0 (during + after)
Part2 ← {+/(¯1↓(100∘|+)\50,⍵){+/0=100|⍺+(×⍵)×⍳|⍵}¨⍵}

⍝ Read and parse input files
input ← ParseRotation¨⊃⎕NGET '2025/data/inputs/01.txt' 1
example ← ParseRotation¨⊃⎕NGET '2025/data/examples/01.txt' 1

⎕← 'Part 1 Example: ',⍕Part1 example
⎕← 'Part 2 Example: ',⍕Part2 example
⎕← 'Part 1: ',⍕Part1 input
⎕← 'Part 2: ',⍕Part2 input