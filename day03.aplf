⍝ AoC 2025 - Day 03 - lobby

⍝ For each bank (row), find the maximum N-digit number
⍝ by choosing N digits in order using greedy selection
GreedySelect ← {
    n data pos len ← ⍺ ⍵ 0 (≢⍵)
    digits ← {
        window ← pos↓(len-(n-⍵))↑data          ⍝ Slice: drop first pos, take up to end
        m ← ⌈/window                            ⍝ Compute max once
        pos ⊢← pos + window⍳m                   ⍝ Move position past the max
        m                                       ⍝ Return max value
    }¨⍳n
    10⊥digits
}

⍝ Read and parse input as 2D matrix of digits
ReadInput ← {⍎¨↑⊃⎕NGET ⍵ 1}

⍝ Sum of max N-digit numbers from each bank (tacit style)
Part1 ← +/∘(2∘GreedySelect¨)∘↓
Part2 ← +/∘(12∘GreedySelect¨)∘↓

⍝ Read input files
input ← ReadInput '2025/data/inputs/03.txt'
example ← ReadInput '2025/data/examples/03.txt'

⎕PP←17
⍝ Test with example input
⎕← 'Part 1 Example: ',⍕Part1 example ⍝ should be 357
⎕← 'Part 2 Example: ',⍕Part2 example ⍝ should be 3121910778619

⍝ Run on actual input
⎕← 'Part 1: ',⍕Part1 input  ⍝ should be 17095
⎕← 'Part 2: ',⍕Part2 input  ⍝ should be 168794698570517